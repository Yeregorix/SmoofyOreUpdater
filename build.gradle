plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.diffplug.spotless' version '6.25.0'
}

group 'net.smoofyuniverse'
version '1.4.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    withJavadocJar()
    withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "utf-8"
}

repositories {
    mavenCentral()
    maven {
        name = 'sponge'
        url = 'https://repo.spongepowered.org/repository/maven-public/'
    }
}

dependencies {
    api 'net.smoofyuniverse:oreapi:1.1.0'
    api 'org.spongepowered:spongeapi:12.0.0'
}

spotless {
    ratchetFrom 'origin/master'

    java {
        licenseHeaderFile(rootProject.file('HEADER'))
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'oreupdater'
            from components.java

            pom {
                name = 'SmoofyOreUpdater'
                packaging = 'jar'
                description = "An update checker using Ore API."
                url = 'https://github.com/Yeregorix/SmoofyOreUpdater'

                scm {
                    connection = 'scm:git:https://github.com/Yeregorix/SmoofyOreUpdater.git'
                    developerConnection = 'scm:git:https://github.com/Yeregorix/SmoofyOreUpdater.git'
                    url = 'https://github.com/Yeregorix/SmoofyOreUpdater'
                }

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id = 'Yeregorix'
                        name = 'Hugo Dupanloup'
                        email = 'yeregorix@smoofyuniverse.net'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            url = version.endsWith('SNAPSHOT') ?
                    "https://oss.sonatype.org/content/repositories/snapshots/"
                    : "https://oss.sonatype.org/service/local/staging/deploy/maven2/"

            credentials {
                username findProperty('ossrhUsername')
                password findProperty('ossrhPassword')
            }
        }
    }
}

if (project.hasProperty("signing.keyId")) {
    apply plugin: 'signing'
    signing {
        sign publishing.publications.mavenJava
    }
}
